from os.path import join
Import('env')

configure = env.Command('configure', 'configure.in', 'autoconf --output=$TARGET $SOURCE')

silent = env.GetOption('silent')
runConfigure = ['cd', '$TARGET.dir', '&&', './$SOURCE.file', 'EXTRASRCDIRS=../post/cil-wpds-xml', 'EXTRAFEATURES=wpds mayMustCall mayMustModifyGlobal']
if silent: runConfigure.append('--silent')
makefile = env.Command('Makefile', configure, [runConfigure])

srcs = []
srcdirs = ['cil/ocamlutil', 'cil/src', 'cil/src/ext', 'cil/src/ext/pta', 'cil/src/frontc', 'post/cil-wpds-xml']
for path in srcdirs:
    subdir = Dir(join('..', path)).abspath
    srcs.append(env.Glob(subdir + '/*.ml'))
    srcs.append(env.Glob(subdir + '/*.ml[ily]'))

deps = [makefile, srcs]
make = ['make', '-C', Dir('.'), 'cilly']
if silent: make += ['--silent', 'NARRATIVE=:']
if env['NATIVECAML']: make.append('NATIVECAML=1')
[cilly] = env.Command('$CILLY', deps, [make])
Default(cilly)
Clean(cilly, [cilly.dir.dir])
